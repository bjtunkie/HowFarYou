buildscript {
    repositories {
        google()
        jcenter()
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }

    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.2.6.RELEASE"
        classpath 'org.openjfx:javafx-plugin:0.0.8'
        classpath "org.beryx:badass-jlink-plugin:2.19.0"
    }
}

allprojects {
    group = 'com.chat'
    version = '0.1'

    ext.appName = 'Project-1989'
    ext.gdxVersion = version

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    ext.coreDependencies = [
            'com.google.code.gson:gson:latest.release',
            'org.hello.lib:hello:latest.release',
            'org.slf4j:slf4j-api:1.7.30',
            'org.slf4j:slf4j-log4j12:latest.release'
    ]
}
rootProject.buildDir = 'build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
    project.evaluationDependsOn(':core')
}

project(":PlayGround") {
    apply plugin: "java"

    dependencies {
        implementation 'com.google.code.gson:gson:latest.release'
    }
}
project(":core") {
    apply plugin: "java"
    apply plugin: 'application'

    sourceCompatibility = '11'
    targetCompatibility = '11'

    mainClassName = 'com.chat.core.Main'
    jar {
        manifest {
            attributes "Main-Class": "$mainClassName"
        }

        from {
            configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
        }
    }


    dependencies {
        implementation coreDependencies

    }
}

project(":desktop") {
    apply plugin: "java"
    apply plugin: "application"
    apply plugin: 'org.openjfx.javafxplugin'
    apply plugin: 'org.beryx.jlink'

    mainClassName = 'com.chat.desktop.ChatApplication'
    jar.manifest {
        attributes(
                'Manifest-Version': '1.0',
                'Main-Class': mainClassName
        )

    }
    javafx {

        sdk = '/home/surya/install/javafx-sdk-11.0.2'
        modules = ['javafx.controls', 'javafx.fxml']
        description = 'This is a sample configuration, it is not real.'
    }

    jlink {
        options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
//        launcher {
//            name = 'chatfx'
//        }

    }


    dependencies {
        implementation coreDependencies
        implementation project(':core')
        implementation 'org.slf4j:slf4j-api:1.7.30'
        implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.13.3'
    }
}

project(":android") {
    apply plugin: 'android'
    apply plugin: 'com.android.application'

    android {
        compileSdkVersion 29
        buildToolsVersion "29.0.3"

        defaultConfig {
            applicationId "com.chat.android"
            minSdkVersion 24
            targetSdkVersion 29
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        }

        buildTypes {
            debug {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
            release {
                minifyEnabled true
                proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            }
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

    }


    dependencies {

        implementation project(":core")
        implementation coreDependencies

        implementation fileTree(dir: 'libs', include: ['*.jar'])
        implementation 'androidx.appcompat:appcompat:1.1.0'
        implementation 'com.google.android.material:material:1.1.0'
        implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
        implementation 'androidx.legacy:legacy-support-v4:1.0.0'
        implementation 'androidx.annotation:annotation:1.1.0'

        implementation 'androidx.legacy:legacy-support-v13:1.0.0'
        testImplementation 'junit:junit:4.12'
        androidTestImplementation 'androidx.test.ext:junit:1.1.1'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    }
}